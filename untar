#!/usr/bin/perl -w
#
# untar
#
# Copyright (C) 2003 Tim Hawes <me@timhawes.com>
#
# Extracts a single tarball, working out which compression tool
# should be used, and handling archives that don't extract cleanly
# to their own directories.
#

use strict;
use File::Basename;
use File::Temp qw/tempdir/;

my $pathname = $ARGV[0];
my $filename = basename($pathname);
my $type;
my $cmdt;
my $cmdx;
my $name;
my $dest;

my $tempdir = tempdir(DIR => '.');

if ( $filename =~ /^(.+)\.tar\.Z$/ ) {
    $name = $1;
    $cmdx = "tar xZpf $pathname -C $tempdir";
} elsif ( $filename =~ /^(.+)\.(tar\.gz|tgz)$/ ) {
    $name = $1;
    $cmdx = "tar xzpf $pathname -C $tempdir";
} elsif ( $filename =~ /^(.+)\.tar\.bz2$/ ) {
    $name = $1;
    $cmdx = "bzcat $pathname | tar xpf - -C $tempdir";
} elsif ( $filename =~ /^(.+)\.tar\.xz$/ ) {
    $name = $1;
    $cmdx = "xzcat $pathname | tar xpf - -C $tempdir";
} else {
    rmdir $tempdir;
    die "File suffix not recognised\n";
}

system $cmdx;

opendir(D, $tempdir);
my @files = grep { ! /^\.\.?$/ } readdir(D);
closedir(D);

if ( -e $name ) {
    my $suffix = 1;
    while ( -e "$name.$suffix" ) {
	$suffix++;
    }
    $dest = "$name.$suffix";
} else {
    $dest = $name;
}

if ( $#files == -1 ) {
    rmdir $tempdir;
    die "No files extracted\n";
} elsif ( $#files == 0 ) {
    if ( $files[0] eq $name ) {
	rename "$tempdir/$name", "$dest";
	rmdir $tempdir;
    } else {
	rename $tempdir, $dest;
    }
} else {
    rename $tempdir, $dest;
}
